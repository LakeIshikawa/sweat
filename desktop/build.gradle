apply plugin: "java"
apply plugin: 'maven-publish'

sourceSets{
    main {
        java.srcDirs = [ "src/", "dev/" ]
    }
    release {
        compileClasspath = sourceSets.main.compileClasspath
        runtimeClasspath = sourceSets.main.runtimeClasspath

        java.srcDirs = [ "src", "../android/assets/resources" ]
    }
}

sourceCompatibility = 1.7

project.ext.mainClassName = "com.lksoft.sweat.desktop.DesktopLauncher"
project.ext.assetsDir = new File("../android/assets");

task run(dependsOn: classes, type: JavaExec) {
    main = project.mainClassName
    classpath = sourceSets.main.runtimeClasspath
    standardInput = System.in
    workingDir = project.assetsDir
    ignoreExitValue = true
}

task dist(type: Jar, dependsOn: [classes]) {
    from files(sourceSets.main.output.classesDir)
    from files(sourceSets.main.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}

    manifest {
        attributes 'Main-Class': project.mainClassName
    }

    baseName = "Sweat"

    doLast {
        // Write batch files
        File bat = new File(destinationDir, "sweat.bat")
        destinationDir.mkdirs();
        bat.write('java -jar sweat\\' + archiveName + ' > sweat\\sweat.log 2>&1')
    }
}

task distRelease(type: Jar) {
    from files(sourceSets.release.output.classesDir)
    from files(sourceSets.release.output.resourcesDir)
    from {configurations.compile.collect {zipTree(it)}}
    from files(project.assetsDir);
    exclude 'sweat/**'
    exclude 'resources/**'

    manifest {
        attributes 'Main-Class': project.mainClassName
    }

    baseName = "Game"
}

distRelease.dependsOn releaseClasses

// Maven
publishing {
    repositories{
        maven{
            url "${project.getProperties().get("publishURL")}"
            credentials{
                username "${System.getenv("USER")}"
                password "${System.getenv("PASS")}"
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId 'sweat-desktop'
            from components.java
        }
    }
}